plugins {
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'org.spongepowered.mixin' version '0.7-SNAPSHOT'
    id 'maven-publish'
}

group = project.maven_group
version = "${project.fabric_version}-${project.mod_version}-${project.minecraft_version}"
archivesBaseName = project.archives_base_name

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven { url = 'https://maven.theillusivec4.top/' }
}

minecraft {
    mappings channel: 'official', version: project.minecraft_version

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'

            mods {
                inmis {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'

            mods {
                inmis {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'inmis', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                inmis {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"
    implementation fg.deobf("top.theillusivec4.curios:curios-forge:${project.curios_version}")
    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
}

mixin {
    add sourceSets.main, "inmis.refmap.json"
    config "inmis.mixins.json"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version, "minecraft_version": project.minecraft_version
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release = 17
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(jar)
            artifact(sourcesJar)
        }
    }
}
