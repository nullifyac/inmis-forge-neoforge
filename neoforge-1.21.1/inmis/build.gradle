plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.78'
}

group = project.maven_group
version = "${project.fabric_version}-${project.mod_version}-${project.minecraft_version}"

base {
    archivesName = project.archives_base_name
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven { url = 'https://maven.theillusivec4.top/' }
}

neoForge {
    version = project.neoforge_version

    runs {
        client {
            client()
        }

        server {
            server()
            programArgument '--nogui'
        }
    }

    mods {
        inmis {
            sourceSet sourceSets.main
        }
    }
}

dependencies {
    compileOnly "top.theillusivec4.curios:curios-neoforge:${project.curios_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "neoforge_version", project.neoforge_version
    inputs.property "curios_version", project.curios_version
    inputs.property "curios_version_range", project.curios_version_range

    filesMatching("META-INF/neoforge.mods.toml") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "neoforge_version": project.neoforge_version,
                "curios_version": project.curios_version,
                "curios_version_range": project.curios_version_range
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release = 21
    options.encoding = 'UTF-8'
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(jar)
            artifact(sourcesJar)
        }
    }
}
