import net.neoforged.gradle.common.tasks.PotentiallySignJar

plugins {
    id 'multiloader-loader'
    id 'net.neoforged.gradle.userdev' version '7.0.167'
}

def at = file('src/main/resources/META-INF/accesstransformer.cfg')
if (at.exists()) {
    minecraft.accessTransformers.file at
}

runs {
    configureEach {
        modSource project.sourceSets.main
    }
    client {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
    }
    server {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        programArgument '--nogui'
    }
    data {
        programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    repositories {
        maven {
            url "https://cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }
    }
}

dependencies {
    implementation "net.neoforged:neoforge:${neoforge_version}"
    implementation "com.mrcrayfish:framework-neoforge:${minecraft_version}-${framework_version}"
    runtimeOnly "com.mrcrayfish:catalogue-neoforge:${minecraft_version}-${catalogue_version}"

    // Integrations
    //runtimeOnly "curse.maven:gravestone-mod-238551:5794085"
    compileOnly "curse.maven:youre-in-grave-danger-544912:6684166"
    compileOnly "curse.maven:cloth-config-348521:5729127"
}

tasks.register('signJar', PotentiallySignJar) {
    dependsOn jar
    onlyIf {
        hasProperty('keyStore') || System.getenv("KEYSTORE")
    }
    keyStore = findProperty('keyStore') ?: System.getenv("KEYSTORE")
    alias = findProperty('keyStoreAlias') ?: System.getenv("KEYSTORE_ALIAS")
    storePass = findProperty('keyStorePass') ?: System.getenv("KEYSTORE_PASS")
    input = jar.archiveFile
}

jar.finalizedBy 'signJar'