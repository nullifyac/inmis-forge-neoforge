import org.apache.commons.io.FilenameUtils

plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
}

repositories {
    exclusiveContent {
        forRepository {
            maven {
                name = 'TerraformersMC'
                url = 'https://maven.terraformersmc.com/'
            }
        }
        filter {
            includeGroup("com.terraformersmc")
        }
    }
    repositories {
        maven {
            url "https://cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    modImplementation "com.mrcrayfish:framework-fabric:${minecraft_version}-${framework_version}"
    modCompileOnlyApi "com.terraformersmc:modmenu:${modmenu_version}"
    modRuntimeOnly "com.mrcrayfish:catalogue-fabric:${minecraft_version}-${catalogue_version}"

    // Integrations
    modCompileOnly "curse.maven:youre-in-grave-danger-544912:6684160"
}

sourceSets.main.resources.srcDirs += "src/generated/resources"

fabricApi {
    configureDataGeneration {
        outputDirectory.set(file("src/generated/resources"))
    }
}

loom {
    def aw = file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("runs/client")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("runs/server")
        }
    }
}

tasks.register("signJar") {
    dependsOn remapJar
    onlyIf {
        findProperty('keyStore') || System.getenv("KEYSTORE")
    }
    doLast {
        def unsignedJar = tasks.remapJar.archiveFile.get()
        def name = FilenameUtils.removeExtension(unsignedJar.getAsFile().getName());
        def signedJar  = layout.buildDirectory.file("libs/%s-signed.jar".formatted(name)).get();
        def options = [:]
        options['jar'] = unsignedJar.getAsFile().getAbsolutePath()
        options['signedJar'] = signedJar.getAsFile().getAbsolutePath()
        options['alias'] = findProperty('keyStoreAlias') ?: System.getenv("KEYSTORE_ALIAS")
        options['keyStore'] = findProperty('keyStore') ?: System.getenv("KEYSTORE")
        options['storePass'] = findProperty('keyStorePass') ?: System.getenv("KEYSTORE_PASS")
        options['storetype'] = "jks"
        this.getAnt().invokeMethod("signjar", options);
    }
}

build.dependsOn signJar
